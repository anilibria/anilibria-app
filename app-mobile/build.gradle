import java.text.DateFormat
import java.text.SimpleDateFormat

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"


static def getDateTime() {
    DateFormat df = new SimpleDateFormat("dd MMMMM yyyy")
    return df.format(new Date()) + " Ð³."
}

android {
    namespace "ru.radiationx.anilibria"

    compileSdk app_compile_sdk_version

    defaultConfig {
        applicationId "ru.radiationx.anilibria.app"
        minSdkVersion app_min_sdk_version
        targetSdkVersion app_target_sdk_version
        versionCode 65
        versionName "2.10.0_RC4"
        buildConfigField "String", 'BUILD_DATE', '"' + getDateTime() + '"'
        vectorDrawables.useSupportLibrary = true
        resourceConfigurations += ['ru']
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions = ["type"]
    productFlavors {
        app {
            dimension "type"
        }

        store {
            dimension "type"
            applicationId "tv.anilibria.store"
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def appName = "AniLibria"
            def versionName = variant.versionName
            outputFileName = "${appName}_v${versionName}.apk"
        }
    }
}

kotlin {
    jvmToolchain(jvm_toolchain_version)
}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint_version"

    implementation project(":data")
    implementation project(":shared-android-ktx")
    implementation project(":shared-app")
    implementation project(":quill-di")

    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.cardview:cardview:$androidx_cardview_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidx_swiperefresh_version"
    implementation "androidx.preference:preference-ktx:$androidx_preference_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    implementation "ru.terrakok.cicerone:cicerone:$cicerone_version"

    //implementation "com.lapism:searchview:4.0"
    //implementation "com.lapism:search:1.0.0"
    implementation project(":lapism-search")

    implementation "com.google.android:flexbox:$flexbox_version"

    implementation "com.devbrackets.android:exomedia:$exomedia_version"

    implementation "org.bsc.util:minitemplator-repackaged:$minitemplator_version"

    implementation "com.hannesdorfmann:adapterdelegates4:$adapterdelegates_version"

    implementation "com.github.mintrocket.MintPermissions:mintpermissions:$mintpermissions_version"
    implementation "com.github.mintrocket.MintPermissions:mintpermissions-flows:$mintpermissions_version"

    implementation "me.zhanghai.android.materialprogressbar:library:$materialprogressbar_version"

    implementation project(":bottomsheet")

    implementation "com.github.stephanenicolas.toothpick:ktp:$toothpick_version"
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothpick_version"

    implementation "at.blogc:expandabletextview:$expandabletextview_version"

    implementation "com.github.stealthcopter:AndroidNetworkTools:$androidnetworktools_version"


    implementation "com.google.firebase:firebase-messaging:$firebase_messaging_version"
    implementation "com.google.firebase:firebase-core:$firebase_core_version"

    implementation "com.github.kirich1409:viewbindingpropertydelegate:$viewbindingdelegate_version"
}

apply plugin: "com.google.gms.google-services"